buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    signingConfigs {
        debug {
            storeFile = file("osirisDebugKeyStore.jks")
            storePassword = 'mydebug'
            keyAlias = 'debug'
            keyPassword ='mydebug'
        }
        release{
            File propFile = file("${projectDir}${File.separator}signing.properties")

            if(propFile.exists()) {
                println 'RootDir signing.properties exists'
                logger.info 'RootDir signing.properties exists'
            } else {
                logger.info "signing.properties does not exist in ${projectDir.toString()}${File.separator}signing.properties"
                println "signing.properties does not exist in ${projectDir.toString()}${File.separator}signing.properties"
            }
            if (propFile.exists()) {
                def Properties props = new Properties()
                props.load(new FileInputStream(propFile))
                if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
                    storeFile = file(props['STORE_FILE'])
                    storePassword = props['STORE_PASSWORD']
                    keyAlias = props['KEY_ALIAS']
                    keyPassword = props['KEY_PASSWORD']
                } else {
                    println 'signing.properties a property is not set'
                    logger.info 'signing.properties a property is not set'
                }
            }
        }
    }

    defaultConfig {
        applicationId "at.pria.osiris.osiris"
        minSdkVersion 10
        targetSdkVersion 17
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                def file = variant.outputFile
                def fileName = "Osiris-App_" + version + ".apk";
                variant.outputFile = new File(file.parent, fileName);
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile project(':api')
}
